;PEREIRA SYSTEM AI - AGE OF EMPIRES II DEFINITIVE

; 1. Constants and timers --------------
;(load "Components/Constants")
; 2. Strategic numbers
; 3. Researching
; 4. Building
; 5. Creating units
; 6. Trade

; 7. Detect things 
(load "Pereira/PereiraSystemComponents/Taunts")
; 1. CONSTANTS AND TIMERS -----------------

(defconst gl-max-lumber-camps-qty 1)
(defconst gl-max-mining-camps-qty 2)

(defconst gl-minimumWoodForTC 10)
(defconst gl-minimumWoodForLC 11)

(defconst gl-dudeHasCavalry 50)
(defconst gl-dudeHasScouts 51)
(defconst gl-dudeHasKnights 52)
(defconst gl-dudeHasCamels 53)
(defconst gl-dudeHasElephants 54)
(defconst gl-dudeHasArchers 55)
(defconst gl-dudeHasSkirmishers 56)
(defconst gl-dudeHasSpearmen 57)
(defconst gl-dudeHasMilitia 58)
(defconst gl-dudeHasCavalryArchers 59)
(defconst gl-dudeHasSiegeWeapons 60)

(defconst gl-maximumSoldiersFeudal 80)
(defconst gl-totalSoldiersCreatedThisAge 89)
(defconst gl-nonUpgradedUniqueUnitsCreated 90)

(defconst gl-weMovedPlace 99)
(defconst gl-weAreAttacking 100)

(defconst gl-maxHousesBeforeDeleting 101)    
(defconst gl-maxHousesNeeded 102)
(defconst gl-farmQtyImperial 103)

; civilian units constants
(defconst gl-maxCiviliansDarkAge 104)
(defconst gl-maxCiviliansFeudalAge 105)
(defconst gl-maxCiviliansCastleAge 106)
(defconst gl-maxCiviliansImperialAge 107)

(defconst gl-tradeCartsQty 108)
(defconst gl-tradeShipsQty 109)

; infantry constants
(defconst gl-spearmenQtyLow 120)
(defconst gl-spearmenQtyMedium 121)
(defconst gl-spearmenQtyHigh 122)

(defconst gl-militiaQtyLow 155)
(defconst gl-militiaQtyMedium 156)
(defconst gl-militiaQtyHigh 157)

(defconst gl-eagleQtyLow 160)
(defconst gl-eagleQtyMedium 161)
(defconst gl-eagleQtyHigh 162)

; ranged constants
(defconst gl-archerQtyLow 200)
(defconst gl-archerQtyMedium 201)
(defconst gl-archerQtyHigh 202)

(defconst gl-skirmisherQtyLow 210)
(defconst gl-skirmisherQtyMedium 211)
(defconst gl-skirmisherQtyHigh 212)

(defconst gl-cavalryArcherQtyLow 220)
(defconst gl-cavalryArcherQtyMedium 221)
(defconst gl-cavalryArcherQty 223)
(defconst gl-cavalryArcherQtyHigh 223)

(defconst gl-handCannoneerQty 225)

; cavalry constants
(defconst gl-knightsQtyLow 130)
(defconst gl-knightsQtyMedium 131)
(defconst gl-knightsQtyHigh 132)

(defconst gl-scoutsQtyLow 135)
(defconst gl-scoutsQtyMedium 136)
(defconst gl-scoutsQtyHigh 137)

(defconst gl-camelQtyLow 140)
(defconst gl-camelQtyMedium 141)
(defconst gl-camelQtyHigh 142)


(defconst gl-battleElephantQtyLow 180)
(defconst gl-battleElephantQtyMedium 181)
(defconst gl-battleElephantQtyHigh 182)
; priest constants
(defconst gl-monkQtyLow 390)
(defconst gl-monkQtyMedium 391)
(defconst gl-monkQtyHigh 392)
; siege constants
(defconst gl-ramQtyLow 400)
(defconst gl-ramQtyMedium 401)
(defconst gl-ramQtyHigh 402)

(defconst gl-scorpionQty 405)

(defconst gl-mangonelQtyLow 410)
(defconst gl-mangonelQtyMedium 411)

(defconst gl-bombardCannonsQty 415)

(defconst gl-trebuchetsQtyLow 420)
(defconst gl-trebuchetsQtyMedium 421)
(defconst gl-trebuchetsQtyHigh 422)

; unique units constants
(defconst gl-uniqueUnitQtyLow 430)
(defconst gl-uniqueUnitQtyMedium 431)
(defconst gl-uniqueUnitQtyHigh 432)

(defconst gl-galleyQty 450)
(defconst gl-galleonQty 455)
(defconst gl-fireshipQty 460)
(defconst gl-demolitionShipQty 465)
(defconst gl-specialShipQty 470)


(defrule
    (true)
=>
    (set-goal gl-maxHousesBeforeDeleting 21)    
    (set-goal gl-maxHousesNeeded 30)
    (set-goal gl-farmQtyImperial 20)

    (set-goal gl-maxCiviliansDarkAge 16)
    (set-goal gl-maxCiviliansFeudalAge 18)
    (set-goal gl-maxCiviliansCastleAge 53)
    (set-goal gl-maxCiviliansImperialAge 85)

    (set-goal gl-tradeCartsQty 14)
    (set-goal gl-tradeShipsQty 20)
)

(defrule
    (true)
=>
    (set-goal gl-spearmenQtyLow 6)
    (set-goal gl-spearmenQtyMedium 35)
    (set-goal gl-spearmenQtyHigh 60)

    (set-goal gl-militiaQtyLow 8)
    (set-goal gl-militiaQtyMedium 20)
    (set-goal gl-militiaQtyHigh 60)

    (set-goal gl-eagleQtyLow 8)
    (set-goal gl-eagleQtyMedium 16)
    (set-goal gl-eagleQtyHigh 40)

    (set-goal gl-archerQtyLow 8)
    (set-goal gl-archerQtyMedium 16)
    (set-goal gl-archerQtyHigh 40)

    (set-goal gl-skirmisherQtyLow 8)    
    (set-goal gl-skirmisherQtyMedium 16)
    (set-goal gl-skirmisherQtyHigh 30)

    (set-goal gl-cavalryArcherQtyLow 12)
    (set-goal gl-cavalryArcherQtyMedium 18)
    (set-goal gl-cavalryArcherQtyHigh 40)

    (set-goal gl-handCannoneerQty 30)

    (set-goal gl-knightsQtyLow 4)
    (set-goal gl-knightsQtyMedium 9)
    (set-goal gl-knightsQtyHigh 40)

    (set-goal gl-scoutsQtyLow 8)
    (set-goal gl-scoutsQtyMedium 30)
    (set-goal gl-scoutsQtyHigh 40)    

    (set-goal gl-camelQtyLow 5)
    (set-goal gl-camelQtyMedium 15)
    (set-goal gl-camelQtyHigh 28)        
)

(defrule
    (true)
=>
    (set-goal gl-monkQtyLow 5)
    (set-goal gl-monkQtyMedium 8)
    (set-goal gl-monkQtyHigh 16)

    (set-goal gl-ramQtyLow 2)
    (set-goal gl-ramQtyMedium 6)
    (set-goal gl-ramQtyHigh 16)

    (set-goal gl-scorpionQty 4)

    (set-goal gl-mangonelQtyLow 4)
    (set-goal gl-mangonelQtyMedium 9)

    (set-goal gl-bombardCannonsQty 8)

    (set-goal gl-uniqueUnitQtyLow 10)
    (set-goal gl-uniqueUnitQtyMedium 24)
    (set-goal gl-uniqueUnitQtyHigh 40)    

    (set-goal gl-trebuchetsQtyLow 5)
    (set-goal gl-trebuchetsQtyMedium 8)    
    (set-goal gl-trebuchetsQtyHigh 12)
)

(defrule
    (true)
=>
    (set-goal gl-galleyQty 20)
    (set-goal gl-galleonQty 20)
    (set-goal gl-fireshipQty 20)
    (set-goal gl-demolitionShipQty 20)
    (set-goal gl-specialShipQty 20)
)

(defrule
    (civ-selected sicilians)
=>
    (up-modify-goal gl-knightsQtyLow c:* 1,8)
    (up-modify-goal gl-knightsQtyMedium c:* 1,8)

    (up-modify-goal gl-scoutsQtyLow c:* 1,8)
    (up-modify-goal gl-scoutsQtyMedium c:* 1,8)
    (disable-self)
)

(defrule
    (civ-selected burgundians)
    ;(civ-selected berbers)
=>
    (up-modify-goal gl-scoutsQtyLow c:+ 4)
    (up-modify-goal gl-skirmisherQtyLow c:- 3)
    (up-modify-goal gl-scoutsQtyLow c:+ 4)
    (up-modify-goal gl-skirmisherQtyMedium c:- 5)    
    (up-modify-goal gl-archerQtyLow c:- 3) 
    (up-modify-goal gl-militiaQtyLow c:- 3)     
    (up-modify-goal gl-knightsQtyLow c:+ 5)   
    (disable-self)
)

(defrule
    (true)
=>    
    (set-goal gl-max-lumber-camps-qty 3)
    (set-goal gl-max-mining-camps-qty 1)
    (set-goal gl-minimumWoodForTC 275)
    (set-goal gl-minimumWoodForLC 100)
    ;(up-chat-data-to-self "Lumber Camps Max Qty = %d" g: gl-max-lumber-camps-qty)
    ;(up-chat-data-to-self "Mining Camps Max Qty = %d" g: gl-max-mining-camps-qty)
    (disable-self)	    
)

(defrule
    (true)
=>    
    (set-goal gl-dudeHasCavalry 0)    
    (set-goal gl-dudeHasScouts 0)
    (set-goal gl-dudeHasKnights 0)
    (set-goal gl-dudeHasCamels 0)
    (set-goal gl-dudeHasElephants 0)
    (set-goal gl-dudeHasArchers 0)
    (set-goal gl-dudeHasCavalryArchers 0)
    (set-goal gl-dudeHasMilitia 0)        
    (set-goal gl-dudeHasSkirmishers 0)
    (set-goal gl-weMovedPlace 0)
    (set-goal gl-weAreAttacking 0)
    (set-goal gl-maximumSoldiersFeudal 30)
    (set-goal gl-totalSoldiersCreatedThisAge 0)
    (set-goal gl-nonUpgradedUniqueUnitsCreated 0)              
    (disable-self)	    
)

(defrule
    (current-age == castle-age)
=>    
    (set-goal gl-totalSoldiersCreatedThisAge 0)          
    (disable-self)	    
)

(defconst gl-civPop 300)
(defrule
	(true)
=>
	(up-get-fact civilian-population 0 gl-civPop)
)

(defconst gl-buildCount 301)
(defrule
	(true)
=>
	(up-get-fact building-count 0 gl-buildCount)
)

(defconst gl-milPop 330)
(defrule
	(true)
=>
	(up-get-fact military-population 0 gl-milPop)
)

(defconst gl-enemyBuildingsInTown 331)
(defrule
	(true)
=>
	(up-get-fact enemy-buildings-in-town 0 gl-enemyBuildingsInTown)
)

(defconst gl-infantrySoldierAmount 332)
(defrule
	(true)
=>
	(up-get-fact soldier-count 0 gl-infantrySoldierAmount)
)

(defconst gl-attackSoldierAmount 333)
(defrule
	(true)
=>
	(up-get-fact attack-soldier-count 0 gl-attackSoldierAmount)
)

(defconst gl-defendSoldierAmount 334)
(defrule
	(true)
=>
	(up-get-fact defend-soldier-count 0 gl-defendSoldierAmount)
)

(defconst gl-warboatAmount 335)
(defrule
	(true)
=>
	(up-get-fact warboat-count 0 gl-warboatAmount)
)

(defconst gl-attackWarboatAmount 336)
(defrule
	(true)
=>
	(up-get-fact attack-warboat-count 0 gl-attackWarboatAmount)
)

(defconst gl-defendWarboatAmount 337)
(defrule
	(true)
=>
	(up-get-fact defend-warboat-count 0 gl-defendWarboatAmount)
)

; TIMER 1 INCREASES TOWN SIZE
(defrule(true)=>(enable-timer 1 720)(disable-self)) 
(defrule
	(timer-triggered 1)
=>
	(disable-timer 1)
    (enable-timer 1 140)  ;120 before  
    (up-modify-sn sn-maximum-town-size c:+ 1)
    (up-modify-sn sn-minimum-town-size c:+ 1)
    (up-chat-data-to-self "Town size max increased = %d" s: sn-maximum-town-size)
    (up-chat-data-to-self "Town size min increased = %d" s: sn-minimum-town-size)
)

; TIMER 2 INCREASES LUMBER CAMPS QUANTITY
(defrule(building-type-count lumber-camp > 0)=>(enable-timer 2 120)(disable-self))
(defrule
	(timer-triggered 2)
=>
	(disable-timer 2)
    (enable-timer 2 65)    
    (up-modify-goal gl-max-lumber-camps-qty c:+ 1) 
    ;(up-chat-data-to-self "Lumber camps max qty increased = %d" g: gl-max-lumber-camps-qty)
)

; TIMER 3 INCREASES LUMBER CAMPS DISTANCE FAST WHEN WE HAVE NONE
(defrule(true)=>(enable-timer 3 320)(disable-self))
(defrule
	(timer-triggered 3)
=>
	(disable-timer 3)
    (enable-timer 3 25)    
    (up-modify-sn sn-lumber-camp-max-distance c:+ 1) 
    ;(up-chat-data-to-self "NO LCs! - Lumber camps max distance increased = %d" s: sn-lumber-camp-max-distance)
)

(defrule
	(building-type-count-total lumber-camp > 0)
=>
	(disable-timer 3)
    (enable-timer 4 90)
    (disable-self)
)

; TIMER 4 INCREASES LUMBER CAMPS DISTANCE SLOWLY AFTER BUILDING ONE
(defrule
	(timer-triggered 4)
=>
	(disable-timer 4)
    (enable-timer 4 85) ; more than 100 too slow    
    (up-modify-sn sn-lumber-camp-max-distance c:+ 1) 
    ;(up-chat-data-to-self "Lumber camps max distance increased = %d" s: sn-lumber-camp-max-distance)
)

; TIMER 5 INCREASES MINING CAMPS QUANTITY
(defrule(building-type-count mining-camp > 0)=>(enable-timer 5 180)(disable-self)) 
(defrule
	(timer-triggered 5)
=>
	(disable-timer 5)
    (enable-timer 5 300) ;was 240     
    (up-modify-goal gl-max-mining-camps-qty c:+ 1)    
    ;(up-chat-data-to-self "Mining camps max qty increased = %d" g: gl-max-mining-camps-qty)	
)

; TIMER 6 INCREASES MINING CAMPS DISTANCE (FAST THEN SLOWER)
(defrule(current-age == feudal-age)=>(enable-timer 6 60)(disable-self))
(defrule
	(timer-triggered 6)
=>
	(disable-timer 6)
    (enable-timer 6 70)    
    (up-modify-sn sn-mining-camp-max-distance c:+ 1) 
    ;(up-chat-data-to-self "Mining camps max distance increased = %d" s: sn-mining-camp-max-distance)
)


; TIMER 11 MILITARY REPORT
(defrule(true)=>(enable-timer 11 90)(disable-self)) 

(defrule
	(timer-triggered 11)(current-age == dark-age)

=>
	(disable-timer 11)
    (enable-timer 11 900)            
    ;(up-chat-data-to-self "TownSize: %d" s: sn-maximum-town-size)     
    (up-chat-data-to-self "BuildingsInTown: %d" g: gl-enemyBuildingsInTown)     
    (up-chat-data-to-self "Def: %d" g: gl-defendSoldierAmount)      
    (up-chat-data-to-self "Atk: %d" g: gl-attackSoldierAmount)
)

(defrule
	(timer-triggered 11)(current-age == feudal-age)
=>
	(disable-timer 11)
    (enable-timer 11 600)           
    ;(up-chat-data-to-self "TownSize: %d" s: sn-maximum-town-size)
    (up-chat-data-to-self "BuildingsInTown: %d" g: gl-enemyBuildingsInTown)       
    (up-chat-data-to-self "Def. / Explore: %d" g: gl-defendSoldierAmount)      
    (up-chat-data-to-self "Attack: %d" g: gl-attackSoldierAmount)
    (up-chat-data-to-self "Inf / Cav created in feudal age: %d" g: gl-totalSoldiersCreatedThisAge)    
)

(defrule
	(timer-triggered 11)(current-age >= castle-age)

=>
	(disable-timer 11)
    (enable-timer 11 600)           
    ;(up-chat-data-to-self "TownSize: %d" s: sn-maximum-town-size)
    (up-chat-data-to-self "BuildingsInTown: %d" g: gl-enemyBuildingsInTown)        
    (up-chat-data-to-self "Def. / Explore: %d" g: gl-defendSoldierAmount)      
    (up-chat-data-to-self "Attack: %d" g: gl-attackSoldierAmount)
    ;(up-chat-data-to-self "Inf / Cav created in castle age: %d" g: gl-totalSoldiersCreatedThisAge)    
)


; TIMER 15 DELETES VILLAGERS
(defrule(true)=>(enable-timer 15 2400)(disable-self)) 
(defrule
	(timer-triggered 15)
    (current-age == imperial-age)(building-type-count town-center > 0)
    (civilian-population > 45)
=>
	(chat-local-to-self "DELETING IDLE VILLAGERS!")
	(disable-timer 15)
    (enable-timer 15 600)    
    (up-delete-idle-units idle-type-villager)
)

; ATTACK TIMER 20
(defrule(true)=>(enable-timer 20 240)(disable-self))

(defrule
    (timer-triggered 20)(military-population > 16)
    (not(town-under-attack))
    (current-age == dark-age)
=>
	(disable-timer 20)
    (enable-timer 20 3)     
    (chat-local-to-self "DARK AGE ATTACK!")
    (set-strategic-number sn-task-ungrouped-soldiers 0)    
    (set-strategic-number sn-number-attack-groups 1)
    (set-strategic-number sn-minimum-attack-group-size 6)
    (set-strategic-number sn-maximum-attack-group-size 8) 
    (set-strategic-number sn-percent-attack-soldiers 100)
    (set-goal gl-weAreAttacking 1)   
    (attack-now)    
)

(defrule
    (current-age == dark-age)(military-population < 12)
    (game-time > 360)(goal gl-weAreAttacking 1)
=>
	(up-retreat-now)(chat-local-to-self "RETREAT!")
    (set-strategic-number sn-number-attack-groups 0)
    (set-strategic-number sn-enemy-sighted-response-distance 1)
    (set-strategic-number sn-task-ungrouped-soldiers 1)
    (set-goal gl-weAreAttacking 0)              
)

(defrule
    (timer-triggered 20)(military-population > 26)
    (current-age == feudal-age)(not(town-under-attack))
=>
	(disable-timer 20)
    (enable-timer 20 3)     
    (chat-local-to-self "FEUDAL AGE ATTACK!")
    (set-strategic-number sn-task-ungrouped-soldiers 0)    
    (set-strategic-number sn-number-attack-groups 4)
    (set-strategic-number sn-minimum-attack-group-size 9)
    (set-strategic-number sn-maximum-attack-group-size 9)     
    (set-strategic-number sn-percent-attack-soldiers 100)
    (set-goal gl-weAreAttacking 1)       
    (attack-now)    
)

(defrule
    (current-age == feudal-age)(military-population < 22)
    (goal gl-weAreAttacking 1)
=>
	(up-retreat-now)(chat-local-to-self "RETREAT!")
    (set-strategic-number sn-number-attack-groups 0)
    (set-strategic-number sn-enemy-sighted-response-distance 1)
    (set-strategic-number sn-task-ungrouped-soldiers 1)
    (set-goal gl-weAreAttacking 0)          
)

(defrule
    (timer-triggered 20)(military-population > 40)
    (current-age == castle-age)(not(town-under-attack))
=>
	(disable-timer 20)
    (enable-timer 20 3)     
    (chat-local-to-self "CASTLE AGE ATTACK!")
    (set-strategic-number sn-task-ungrouped-soldiers 0)      
    (set-strategic-number sn-number-attack-groups 4)
    (set-strategic-number sn-minimum-attack-group-size 9)
    (set-strategic-number sn-maximum-attack-group-size 9)  
    (set-strategic-number sn-percent-attack-soldiers 100)
    (set-goal gl-weAreAttacking 1)        
    (attack-now)    
)

(defrule
    (current-age == feudal-age)(military-population < 36)
    (goal gl-weAreAttacking 1)
=>
	(up-retreat-now)(chat-local-to-self "RETREAT!")
    (set-strategic-number sn-number-attack-groups 0)
    (set-strategic-number sn-enemy-sighted-response-distance 1)
    (set-strategic-number sn-task-ungrouped-soldiers 1)
    (set-goal gl-weAreAttacking 0)       
)

(defrule(true)=>(enable-timer 21 2100)(disable-self)) ;35 min
(defrule
    (or
        (timer-triggered 21)
        (and(civ-selected burgundians)(research-completed my-unique-research))
    )
    (current-age == imperial-age)
    (population-headroom == 0)
    (housing-headroom == 0)
    (wood-amount > 800)(food-amount > 800)
=>
	(disable-timer 21)
    (enable-timer 21 3)
    (chat-local-to-self "IMPERIAL AGE ATTACK!")
    (set-strategic-number sn-number-attack-groups 1)
    (set-strategic-number sn-minimum-attack-group-size 1)
    (set-strategic-number sn-maximum-attack-group-size 150)
    (set-strategic-number sn-percent-attack-soldiers 100)
    (set-goal gl-weAreAttacking 1)      
    (attack-now)    
)

(defrule
    (current-age == imperial-age)(military-population < 40)
=>
	(up-retreat-now)(chat-local-to-self "RETREAT!")
    (set-goal gl-weAreAttacking 0)        
)

(defrule 
    (military-population > 25)
    (not(town-under-attack))
=>
    (set-strategic-number sn-task-ungrouped-soldiers 1)
    (chat-local-to-self "Soldiers Spread!")
    (disable-self)
)

; 2. STRATEGIC NUMBERS ==================
(defrule
    (true)
=>  (set-strategic-number sn-do-not-scale-for-difficulty-level 1)
    (disable-self)
)

; COMBAT                             
; DEFENSE 
(defrule 
    (true)
=>
    (set-strategic-number sn-allow-civilian-defense 1)         
    (disable-self)
)

(defrule 
    (game-time > 360)(military-population < 2)
=>
    (set-strategic-number sn-allow-civilian-defense 2)         
)

(defrule 
    (true)
=>
    (set-difficulty-parameter ability-to-dodge-missiles 0)
	(set-difficulty-parameter ability-to-maintain-distance 0) 
    (set-strategic-number sn-defense-distance 20)
    (set-strategic-number sn-task-ungrouped-soldiers 0)         
    (disable-self)
)

(defrule ;DEFENSE PRIORITIES
	(true)
=>
	(up-set-defense-priority c: lumber-camp c: 20000)
	(up-set-defense-priority c: mining-camp c: 20000)    
	(up-set-defense-priority c: farm c: 20000)       
	(disable-self)
)

(defrule ;OFFENSE PRIORITIES
	(true)
=>
    (up-set-offense-priority c: university c: 1)
    (up-set-offense-priority c: blacksmith c: 1)    
    (up-set-offense-priority c: lumber-camp c: 2)
    (up-set-offense-priority c: mining-camp c: 2)    
    (up-set-offense-priority c: siege-workshop c: 3)  
    (up-set-offense-priority c: house c: 4)
    (up-set-offense-priority c: villager c: 4)    
    (up-set-offense-priority c: town-center c: 5)
	(up-set-offense-priority c: castle c: 6)      

	(up-set-offense-priority c: cavalry-class c: 11)
	(up-set-offense-priority c: archery-class c: 5)    
	(disable-self)
)

; ATTACK    
; CHOOSING TARGET 
(defrule 
    (true)
=>
    (set-strategic-number sn-free-siege-targeting 0)
    (set-strategic-number sn-enemy-sighted-response-distance 10)     
    (set-strategic-number sn-wall-targeting-mode 1)
    (set-strategic-number sn-attack-intelligence 1)
    (set-strategic-number sn-target-evaluation-distance 10)
    (set-strategic-number sn-coop-share-attacking 1)
    (set-strategic-number sn-target-evaluation-boat 10)    
    (disable-self)
)
(defrule 
    (true)
=>
    (set-strategic-number sn-enable-offensive-priority 1) 
    (set-strategic-number sn-local-targeting-mode 1)     
    (set-strategic-number sn-ignore-attack-group-under-attack 0)        
    (set-strategic-number sn-enable-patrol-attack 1)
    (set-strategic-number sn-percentage-explore-exterminators 50)  

    (set-strategic-number sn-disable-defend-groups 0)  

    (set-strategic-number sn-relic-return-distance 20)
    (set-strategic-number sn-allow-civilian-offense 1)  
    ;(set-strategic-number sn-number-civilian-militia 100) 
	(set-strategic-number sn-garrison-rams 1)  
    (set-strategic-number sn-gather-idle-soldiers-at-center 1)     	
    (disable-self)
)

(defrule 
    (true)
=>
    (set-strategic-number sn-group-form-distance 24) ;def 20
    (set-strategic-number sn-attack-group-gather-spacing 2)     
    (set-strategic-number sn-percent-attack-soldiers 0)
    (set-strategic-number sn-attack-group-size-randomness 0)    
    
    (set-strategic-number sn-minimum-attack-group-size 8)
    (set-strategic-number sn-maximum-attack-group-size 8)
    (set-strategic-number sn-number-attack-groups 0)                    
    (disable-self)
)

; BOATS
(defrule 
    (true)
=>
    (set-strategic-number sn-minimum-boat-attack-group-size 5)
    (set-strategic-number sn-maximum-boat-attack-group-size 18)   
    (set-strategic-number sn-number-boat-attack-groups 1) 
    (disable-self)
)

; EXPLORE 
(defrule 
    (true)
=>  
    (set-strategic-number sn-total-number-explorers 1) 
    (set-strategic-number sn-number-explore-groups 1)
    (set-strategic-number sn-blot-exploration-map 0)
    (disable-self)
)

; SCOUT STYLES
(defrule
	(true)
=>
	;(up-send-scout group-type-land-explore scout-center)
    ;(chat-to-player my-player-number "SCOUTING STYLE CENTER")     
	;(up-send-scout group-type-land-explore scout-opposite)
	(up-send-scout group-type-land-explore scout-corner)
    ;(chat-to-player my-player-number "SCOUTING STYLE CORNER")      
	;(up-send-scout group-type-land-explore scout-enemy)          
 	;(up-send-scout group-type-land-explore scout-border)
    ;(chat-to-player my-player-number "SCOUTING STYLE BORDER")     
	;(up-send-scout group-type-land-explore scout-mirror)
	;(up-send-scout group-type-land-explore scout-flank) 
    ;(chat-to-player my-player-number "SCOUTING STYLE FLANK")      
	(disable-self)
)

(defrule
    (game-time > 120)
=>
    ;(up-send-scout group-type-land-explore scout-flank) 
    ;(chat-to-player my-player-number "SCOUTING STYLE FLANK")        
    (disable-self)
)

(defrule
    (game-time > 150)
=>
    (up-send-scout group-type-land-explore scout-corner)
    ;(chat-to-player my-player-number "SCOUTING STYLE CORNER")          
    (disable-self)
)

(defrule
    (game-time > 180)
=>
    (up-send-scout group-type-land-explore scout-border)
    ;(chat-to-player my-player-number "SCOUTING STYLE BORDER")      
    (disable-self)
)

(defrule
    (game-time > 210)
=>
    (up-send-scout group-type-land-explore scout-flank)
    ;(chat-to-player my-player-number "SCOUTING STYLE FLANK")        
    (disable-self)
)

(defrule
    (game-time > 230)
=>
    (up-send-scout group-type-land-explore scout-corner)
    ;(chat-to-player my-player-number "SCOUTING STYLE CORNER")        
    (disable-self)
)

(defrule
    (game-time > 250)
=>
    (up-send-scout group-type-land-explore scout-center)
    ;(chat-to-player my-player-number "SCOUTING STYLE CENTER")        
    (disable-self)
)

; EXPLORE GROUPS
(defrule 
    (current-age == castle-age)(not(town-under-attack))
=>
    (set-strategic-number sn-total-number-explorers 3) 
    (set-strategic-number sn-number-explore-groups 3)
    (disable-self)
)

(defrule 
    (hold-relics)
=>
    (set-strategic-number sn-total-number-explorers 0) 
    (set-strategic-number sn-number-explore-groups 0)
    (disable-self)
)

; HUNTING
(defrule 
    (true)
=>
    (set-strategic-number sn-enable-boar-hunting 0)
    (set-strategic-number sn-minimum-boar-lure-group-size 3)
    (set-strategic-number sn-minimum-boar-hunt-group-size 6)
    (set-strategic-number sn-minimum-number-hunters 6)
    (disable-self)
)

(defrule 
    (unit-type-count-total villager > 7)
=>
    (set-strategic-number sn-enable-boar-hunting 1)
    (disable-self)
)

; BUILDING 
(defrule
    (true)
=>
    (set-strategic-number sn-initial-exploration-required 1)
    (set-strategic-number sn-minimum-water-body-size-for-dock 3)
    (set-strategic-number sn-dock-avoidance-factor 1000)
    (set-strategic-number sn-dock-proximity-factor 10)
    (set-strategic-number sn-dock-placement-mode 1)
    (disable-self)
)

; EXTRA BUILDERS
(defrule
    (true)
=>
    (up-assign-builders c: monastery c: 3)
    (up-assign-builders c: archery-range c: 1)
    (up-assign-builders c: stable c: 1)    
    (up-assign-builders c: market c: 1)
    (up-assign-builders c: blacksmith c: 1)
    (up-assign-builders c: university c: 1)
    (up-assign-builders c: siege-workshop c: 1)
    (up-assign-builders c: watch-tower c: 1)
    (up-assign-builders c: guard-tower c: 1)       
    (up-assign-builders c: keep c: 2)
    (up-assign-builders c: bombard-tower c: 2)     
    (up-assign-builders c: donjon c: 1)
    (up-assign-builders c: castle c: 5)
    (up-assign-builders c: town-center c: 3)
    (up-assign-builders c: town-center-foundation c: 8)    
    (up-assign-builders c: palisade-wall c: 3)     
    (disable-self)
)

(defrule
    (current-age == castle-age)
=>
    (up-assign-builders c: house c: 2)
    (up-assign-builders c: watch-tower c: 2)
    (up-assign-builders c: guard-tower c: 2)       
    (disable-self)
)

(defrule
    (unit-type-count villager > 40)
=>
    (up-assign-builders c: castle c: 8)
    (up-assign-builders c: watch-tower c: 3)
    (up-assign-builders c: guard-tower c: 3)
    (up-assign-builders c: keep c: 3)
    (up-assign-builders c: bombard-tower c: 3)             
    (disable-self)
)

(defrule
    (building-type-count castle > 0)
=>
    (up-assign-builders c: castle c:+ 2)   
    (disable-self)
)

; TOWN SIZE AND TOWN CENTER
(defrule 
    (true)
=>
    (set-strategic-number sn-minimum-town-size 0)
    (set-strategic-number sn-maximum-town-size 8)
    (set-strategic-number sn-town-center-placement 0)          
    (disable-self)
)

; DROPSITES AND DISTANCES
; CANCEL SITE BUILDING IF DANGEROUS
(defrule 
    (true)
=>
    (set-strategic-number sn-defer-dropsite-update 1)
    (disable-self)
)

(defrule 
    (true)
=>
    (set-strategic-number sn-dropsite-separation-distance 3) ; WORKS?
    (set-strategic-number sn-allow-adjacent-dropsites 0) ; WILL CHANGE LATER
    (set-strategic-number sn-required-forest-tiles 6)
    (disable-self)
)

; CAMP MAX DISTANCES AT THE START
(defrule 
    (true)
=>  
    (set-strategic-number sn-lumber-camp-max-distance 12); 
    (set-strategic-number sn-mining-camp-max-distance 12);     
    (set-strategic-number sn-mill-max-distance 12)  
    (disable-self)
)

(defrule 
    (enemy-buildings-in-town)
=>     
    (up-modify-sn sn-mill-max-distance c:+ 16)  
    (disable-self)
)

(defrule 
    (enemy-buildings-in-town)
=>
    (set-strategic-number sn-lumber-camp-max-distance 38)      
    (disable-self)
)

; INDUSTRIAL MODE LUMBER
(defrule 
    (building-type-count-total lumber-camp > 3)
=>  
    (set-strategic-number sn-maximum-wood-drop-distance 3)
    ;(set-strategic-number sn-required-forest-tiles 8)
    (set-strategic-number sn-allow-adjacent-dropsites 1)       
    (up-chat-data-to-self "Industrial lumber mode activated: Adjacent sites allowed. Max drop distance = %d" s: sn-maximum-wood-drop-distance)
    (disable-self)
)

(defrule 
    (building-type-count-total lumber-camp < 3)
=>  
    (set-strategic-number sn-maximum-wood-drop-distance 9)
)

; PERCENT VILLAGER BUILDERS
(defrule
    (current-age == dark-age)
=>
    (set-strategic-number sn-percent-civilian-explorers 0)
    (set-strategic-number sn-percent-civilian-builders 100)
    (set-strategic-number sn-cap-civilian-builders 12)
    (disable-self)
)    

; PERCENT GATHERERS AND DROP DISTANCES
(defrule
    (current-age == dark-age)
=>
    (set-strategic-number sn-food-gatherer-percentage 70)
    (set-strategic-number sn-wood-gatherer-percentage 30)
    (set-strategic-number sn-gold-gatherer-percentage 0)
    (set-strategic-number sn-stone-gatherer-percentage 0)
    (disable-self)
)

(defrule 
    (current-age == dark-age)
=>  
    (set-strategic-number sn-maximum-hunt-drop-distance 16)
    (set-strategic-number sn-maximum-food-drop-distance 9)
    (set-strategic-number sn-maximum-wood-drop-distance 9)
    (set-strategic-number sn-maximum-gold-drop-distance 0)
    (set-strategic-number sn-maximum-stone-drop-distance 0)
    (disable-self)
)

(defrule 
    (current-age == dark-age)(civ-selected mongol)
=>  
    (set-strategic-number sn-maximum-hunt-drop-distance 26)
    (disable-self)
)

(defrule 
    (current-age == dark-age)
    (or(civ-selected incan)(or(civ-selected mayan)(civ-selected aztec)))
    (game-time > 420)
=>  
    (set-strategic-number sn-maximum-hunt-drop-distance 16)
    (set-strategic-number sn-maximum-food-drop-distance 9)
    (set-strategic-number sn-maximum-wood-drop-distance 9)
    (set-strategic-number sn-maximum-gold-drop-distance 14)
    (set-strategic-number sn-maximum-stone-drop-distance 0)
    (chat-to-player my-player-number "Gold and Stone mining avaiable")    
    (disable-self)
)

(defrule 
    (current-age == dark-age)
    (civ-selected poles)
    (game-time > 420)
=>  
    (set-strategic-number sn-maximum-gold-drop-distance 14)
    (set-strategic-number sn-maximum-stone-drop-distance 14)
    (chat-to-player my-player-number "Gold and Stone mining avaiable")    
    (disable-self)
)

(defrule 
    (current-age == dark-age)
    (game-time > 540)
=>  
    (set-strategic-number sn-maximum-gold-drop-distance 14)
    (chat-to-player my-player-number "LetÂ´s Get Some Gold")
    (disable-self)
)

(defrule
    (up-research-status c: feudal-age == research-pending)
=>
    (set-strategic-number sn-food-gatherer-percentage 40)
    (set-strategic-number sn-wood-gatherer-percentage 50)
    (set-strategic-number sn-gold-gatherer-percentage 10) ;
    (set-strategic-number sn-stone-gatherer-percentage 0)
    (set-strategic-number sn-maximum-gold-drop-distance 14)    
    (disable-self)
)

(defrule 
    (current-age == feudal-age)
=>  
    (set-strategic-number sn-maximum-hunt-drop-distance 16)
    (set-strategic-number sn-maximum-food-drop-distance 6) ;to make them use mills
    (set-strategic-number sn-maximum-stone-drop-distance 0)
    (set-strategic-number sn-food-gatherer-percentage 50)
    (set-strategic-number sn-wood-gatherer-percentage 40)
    (set-strategic-number sn-gold-gatherer-percentage 10) ;    
    (disable-self)
)

(defrule
    (current-age == feudal-age)
    (or(building-type-count blacksmith > 0) ;goths use only barracks
    (building-type-count stable > 0))
    (or(building-type-count market > 0)
    (building-type-count archery-range > 0))    
    (building-type-count-total lumber-camp > 1)    
=>
    (set-strategic-number sn-food-gatherer-percentage 60)
    (set-strategic-number sn-wood-gatherer-percentage 28)
    (set-strategic-number sn-gold-gatherer-percentage 12)
    (set-strategic-number sn-stone-gatherer-percentage 0)    
    (set-strategic-number sn-maximum-gold-drop-distance 6)    
    (disable-self)
)

; DROP DISTANCE CASTLE AGE
(defrule 
    (up-research-status c: castle-age == research-pending)
=>  
    (set-strategic-number sn-enable-boar-hunting 0); no more hunting
    (set-strategic-number sn-minimum-number-hunters 0)
    (set-strategic-number sn-maximum-hunt-drop-distance 0)
    (set-strategic-number sn-maximum-wood-drop-distance 8) 
    (set-strategic-number sn-maximum-gold-drop-distance 9)    
    (set-strategic-number sn-food-gatherer-percentage 56)
    (set-strategic-number sn-wood-gatherer-percentage 28)
    (set-strategic-number sn-gold-gatherer-percentage 16)
    (set-strategic-number sn-stone-gatherer-percentage 0)
    (disable-self)
)

(defrule 
    (current-age == castle-age)
    (building-type-count monastery > 0)
    (building-type-count siege-workshop > 0)    
    (or
        (building-type-count university > 0)
        (building-type-count town-center > 1)         
    )
=>  
    (set-strategic-number sn-food-gatherer-percentage 57)
    (set-strategic-number sn-wood-gatherer-percentage 24)
    (set-strategic-number sn-gold-gatherer-percentage 13)
    (set-strategic-number sn-stone-gatherer-percentage 6)   
    (disable-self)
)

(defrule 
    (current-age == castle-age)
    (wood-amount > 1200)
=>  
    (up-modify-sn sn-food-gatherer-percentage c:+ 16)
    (up-modify-sn sn-wood-gatherer-percentage c:- 16)
     (disable-self)   
)

(defrule 
    (current-age == castle-age)
    (gold-amount > 1200)
=>  
    (up-modify-sn sn-food-gatherer-percentage c:+ 16)
    (up-modify-sn sn-gold-gatherer-percentage c:- 16)  
     (disable-self)
)

(defrule
    (up-research-status c: imperial-age == research-pending)
=>
    (set-strategic-number sn-food-gatherer-percentage 61)
    (set-strategic-number sn-wood-gatherer-percentage 20)
    (set-strategic-number sn-gold-gatherer-percentage 13)
    (set-strategic-number sn-stone-gatherer-percentage 6)  
    (disable-self)
)

(defrule
    (current-age == imperial-age)(not(hold-relics))
=>
    (set-strategic-number sn-food-gatherer-percentage 75)
    (set-strategic-number sn-wood-gatherer-percentage 9)
    (set-strategic-number sn-gold-gatherer-percentage 10)
    (set-strategic-number sn-stone-gatherer-percentage 6)
    (chat-local-to-self  "Imperial Age With No Relics - Sending More Villagers For Gold")     
    (disable-self)
)

(defrule
    (current-age == imperial-age)(hold-relics)
=>
    (set-strategic-number sn-food-gatherer-percentage 77)
    (set-strategic-number sn-wood-gatherer-percentage 13)
    (set-strategic-number sn-gold-gatherer-percentage 4)
    (set-strategic-number sn-stone-gatherer-percentage 6)
    (chat-local-to-self "Imperial Age With All Relics - Sending Less Villagers For Gold")     
    (disable-self)
)

(defrule
    (current-age == imperial-age)
    (not(civ-selected cumans)); to get mercenaries
    (not(civ-selected sicilians)) ;to build donjons
    (not(civ-selected bulgarians))    
    (building-type-count castle > 0)    
=>
    (set-strategic-number sn-stone-gatherer-percentage 0)    
    (set-strategic-number sn-maximum-stone-drop-distance 0)
    (chat-local-to-self "CASTLE BUILT! STONE DISTANCE 0, PERCENTAGE 0")      
    (disable-self)
)

(defrule
    (current-age == imperial-age)(food-amount > 400)
    (building-type-count market > 0)    
    (or
        (building-type-count town-center > 2)
        (building-type-count barracks > 2)
    )
=>
    (set-strategic-number sn-maximum-stone-drop-distance 9)
    (chat-local-to-self "TWO TOWN CENTERS, 2 BARRACK: STONE DISTANCE 9 AGAIN")     
    (disable-self)
)

; CHANGE PERCENTAGES WHEN TOO MANY OF SOMETHING - THEY TEND TO GET MORE GOLD THAN NEEDED
; EXCESS FOOD
(defrule
    (food-amount > 2500)(wood-amount < 500)
=>
    (up-modify-sn sn-food-gatherer-percentage c: -16)
    (up-modify-sn sn-wood-gatherer-percentage c: +16)
    (chat-local-to-self  "We Need More Wood, Less Food")     
    (disable-self)
)

(defrule
    (food-amount > 2600)(gold-amount < 500)
=>
    (up-modify-sn sn-food-gatherer-percentage c: -8)
    (up-modify-sn sn-gold-gatherer-percentage c: +8)
    (chat-local-to-self  "We Need More Gold, Less Food")     
    (disable-self)
)

; EXCESS WOOD
(defrule
    (wood-amount > 2500)(food-amount < 500)
=>
    (up-modify-sn sn-wood-gatherer-percentage c: -16)
    (up-modify-sn sn-food-gatherer-percentage c: +16)
    (chat-local-to-self  "We Need More Food, Less Wood")     
    (disable-self)
)

(defrule
    (wood-amount > 2600)(gold-amount < 500)
=>
    (up-modify-sn sn-wood-gatherer-percentage c: -8)
    (up-modify-sn sn-gold-gatherer-percentage c: +8)
    (chat-local-to-self  "We Need More Gold, Less Wood")     
    (disable-self)
)

; EXCESS GOLD
(defrule
    (gold-amount > 1600)(food-amount < 500)
=>
    (up-modify-sn sn-gold-gatherer-percentage c: -16)
    (up-modify-sn sn-food-gatherer-percentage c: +16)    
    (chat-local-to-self "We Need More Food, Less Gold")     
    (disable-self)
)

(defrule
    (gold-amount > 1600)(food-amount < 500)
=>
    (up-modify-sn sn-gold-gatherer-percentage c: -16)
    (up-modify-sn sn-wood-gatherer-percentage c: +16)    
    (chat-local-to-self "We Need More Wood, Less Gold")     
    (disable-self)
)

(defrule
	(current-age == imperial-age)
=>
	(up-retreat-now)
    (set-strategic-number sn-task-ungrouped-soldiers 1)
    (chat-local-to-self "IMPERIAL AGE, EVERYONE GO BACK!")
	(disable-self)
)

(defrule
	(current-age == imperial-age)
    (population-headroom == 0)
    (housing-headroom == 0)
=>
	(up-retreat-now)
    (set-strategic-number sn-task-ungrouped-soldiers 1)
    (chat-local-to-self "MAX POP, EVERYONE GO BACK!")
	(disable-self)
)

(defrule
	(current-age == imperial-age)
    (research-completed my-unique-research)
=>
	(up-retreat-now)
    (set-strategic-number sn-task-ungrouped-soldiers 1)
    (chat-local-to-self "UNIQUE RESEARCH COMPLETE, EVERYONE GO BACK!")
	(disable-self)
)

(defrule
	(current-age == imperial-age)
    (research-completed my-unique-unit-upgrade)
=>
	(up-retreat-now)
    (set-strategic-number sn-task-ungrouped-soldiers 1)
    (chat-local-to-self "UNIQUE UNIT UPGRADE COMPLETE, EVERYONE GO BACK!")
	(disable-self)
)

(defrule
	(current-age == imperial-age)
    (military-population < 40)
    (players-building-type-count focus-player castle > 0)
=>
	(up-retreat-now)
    (set-strategic-number sn-task-ungrouped-soldiers 1)
    (chat-local-to-self "ENEMY HAS CASTLE, MIL POP LOW, GO BACK!")
	(disable-self)
)

(defrule
    (unit-type-count monk > 0)
=>	
    (up-full-reset-search)	
    (up-find-local c: spearman-line c: 1)
	;(up-find-local c: all-units-class c: 240)	
	;(up-remove-objects search-local object-data-hitpoints < 50)
    (up-guard-unit monk c: spearman-line)
)

(defrule
	(current-age == imperial-age)
    (population-headroom == 0)
    (housing-headroom == 0)
    (wood-amount > 1000)(food-amount > 1000)(gold-amount > 1000)
    (game-time > 2100); 35 min
    (unit-type-count skirmisher-line > 0)
    (can-train hand-cannoneer)    
=>
	(delete-unit skirmisher-line)
    (train hand-cannoneer)
    (chat-local-to-self "'CHANGING' SKIRMISHERS FOR HAND CANNONEERS")
)

(defrule
	(current-age == imperial-age)
    (population-headroom == 0)
    (housing-headroom == 0)
    (wood-amount > 1000)(food-amount > 1000)(gold-amount > 1000)
    (game-time > 2100); 35 min
    (not(civ-selected gothic))(not(civ-selected japanese))    
    (unit-type-count spearman-line > 0)
    (can-train knight-line)    
=>
	(delete-unit spearman-line)
    (train knight-line)
    (chat-local-to-self "'CHANGING' SPEARMEN FOR KNIGHTS")
)